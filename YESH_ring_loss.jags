
model {

# -------------------------------------------------
# Parameters:
# phi: survival probability 
# loss: ring loss probability
# pr: resight probability with ring
# pl: resight probability without ring
# -------------------------------------------------
# States (S):
# 1 alive with ring
# 2 alive without ring
# 3 dead
# Observations (O):  
# 1 seen with ring 
# 2 seen without ring
# 3 not seen
# -------------------------------------------------

# Priors and constraints
   phi ~ dunif(0.7, 1)    # Priors for state-spec. survival
   loss ~ dunif(0, 0.2)    # Priors for ring loss
   pr ~ dunif(0, 1)      # Priors for mean state-spec. recapture
   pl ~ dunif(0, 0.4)      # Priors for mean state-spec. recapture

# Define state-transition and observation matrices
   # Define probabilities of state S(t+1) [last dim] given S(t) [first dim]
      ps[1,1] <- phi * (1 - loss)
      ps[1,2] <- phi * loss
      ps[1,3] <- 1 - phi
      ps[2,1] <- 0
      ps[2,2] <- phi
      ps[2,3] <- 1 - phi
      ps[3,1] <- 0
      ps[3,2] <- 0
      ps[3,3] <- 1
      
      # Define probabilities of O(t) [last dim] given S(t)  [first dim]
      po[1,1] <- pr
      po[1,2] <- 0
      po[1,3] <- 1 - pr
      po[2,1] <- 0
      po[2,2] <- pl
      po[2,3] <- 1 - pl
      po[3,1] <- 0
      po[3,2] <- 0
      po[3,3] <- 1

# Likelihood 
for (i in 1:nind){
   # Define latent state at first capture
   z[i,f[i]] <- y[i,f[i]]
   for (t in (f[i]+1):n.occasions){
      # State process: draw S(t) given S(t-1)
      z[i,t] ~ dcat(ps[z[i,t-1], 1:3])
      # Observation process: draw O(t) given S(t)
      y[i,t] ~ dcat(po[z[i,t], 1:3])
      } #t
   } #i
}
