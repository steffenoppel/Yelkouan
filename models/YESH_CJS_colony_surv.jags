
    model {
    
    # -------------------------------------------------
    # Parameters:
    # phi: survival probability, only adults 
    # p: recapture probability when breeding, depends on colony-occasion-specific effort
    # -------------------------------------------------
    
    
    ## Priors and constraints
    
    ### SITE-SPECIFIC CAPTURE PROBABILITY
    mean.p ~ dbeta(1.5, 4)                        # Prior for mean recapture switched to beta from unif
    logit.p <- log(mean.p / (1-mean.p))           # Logit transformation

    for (col in 1:n.cols) {
      for (s in 1:n.sites){
        beta.effort[s,col] ~ dunif(0,10)                   # Prior for trapping effort offset on capture probability
        for (t in 1:n.years){
          logit(p[s,t,col]) <- logit.p + beta.effort[s,col]*effmat[s,t,col] + capt.raneff[col,t]   ## includes site-specific effort and random effect for time and individual
        } # close t
      } #s
    } #col
    
    ## RANDOM INDIVIDUAL EFFECT ON CAPTURE PROBABILITY
    for (col in 1:n.cols) {
       for (t in 1:n.years){
          capt.raneff[col,t] ~ dnorm(0, tau.capt)
        }
      }
    
    ### PRIORS FOR RANDOM EFFECTS
    sigma.capt ~ dunif(0, 10)                     # Prior for standard deviation of capture
    tau.capt <- pow(sigma.capt, -2)

    
    ### COLONY- AND YEAR-SPECIFIC SURVIVAL PROBABILITY
    for (t in 1:(n.years-1)){
      for (col in 1:n.cols) {
        ann.surv[t,col] ~ dunif(0.7, 1)                      # Priors for colony-specific ANNUAL survival - this is the basic survival that is scaled to difference between occasions which are site-specific
        for (s in 1:n.sites){
          phi[s,t,col] <- pow(pow(ann.surv[t,col],(1/365)),periods[s,t,col]) 
        } #s
      } #col
    } #t
    
    
    # Likelihood 
    for (i in 1:n.ind){

        # Define latent state at first capture
        z[i,f[i]] <- 1
          for (t in (f[i]+1):n.years){

          # State process
            z[i,t] ~ dbern(mu1[i,t])
            mu1[i,t] <- phi[sitevec[i],t-1,colvec[i]] * z[i,t-1] 
    
          # Observation process
            y[i,t] ~ dbern(mu2[i,t])
            mu2[i,t] <- p[sitevec[i],t,colvec[i]] * z[i,t]
          } #t
    } #i
    
    }
    
